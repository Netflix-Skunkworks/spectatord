load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library", "cc_test")

config_setting(
    name = "nflx_internal",
    define_values = {
        "nflx_internal": "1",
    },
)

config_setting(
    name = "centos7",
    define_values = {
        "centos7": "1",
    },
)

cc_library(
    name = "util",
    srcs = ["util/logger.cc"],
    hdrs = [
        "util/files.h",
        "util/logger.h",
    ],
    deps = ["@com_github_gabime_spdlog//:spdlog"],
)

cc_library(
    name = "spectator",
    srcs = [
        "spectator/common_refs.cc",
        "spectator/compressed_buffer.cc",
        "spectator/counter.cc",
        "spectator/dist_summary.cc",
        "spectator/gauge.cc",
        "spectator/gzip.cc",
        "spectator/http_client.cc",
        "spectator/max_gauge.cc",
        "spectator/monotonic_counter.cc",
        "spectator/monotonic_sampled.cc",
        "spectator/percentile_buckets.cc",
        "spectator/registry.cc",
        "spectator/smile.cc",
        "spectator/string_pool.cc",
        "spectator/strings.cc",
        "spectator/timer.cc",
    ],
    hdrs = [
        "percentile_bucket_tags.inc",
        "percentile_bucket_values.inc",
        "spectator/age_gauge.h",
        "spectator/atomicnumber.h",
        "spectator/common_refs.h",
        "spectator/compressed_buffer.h",
        "spectator/config.h",
        "spectator/counter.h",
        "spectator/detail/perc_policy.h",
        "spectator/dist_stats.h",
        "spectator/dist_summary.h",
        "spectator/gauge.h",
        "spectator/gzip.h",
        "spectator/http_client.h",
        "spectator/id.h",
        "spectator/log_entry.h",
        "spectator/max_gauge.h",
        "spectator/measurement.h",
        "spectator/meter.h",
        "spectator/monotonic_counter.h",
        "spectator/monotonic_sampled.h",
        "spectator/percentile_buckets.h",
        "spectator/percentile_distribution_summary.h",
        "spectator/percentile_timer.h",
        "spectator/publisher.h",
        "spectator/registry.h",
        "spectator/smile.h",
        "spectator/string_intern.h",
        "spectator/string_pool.h",
        "spectator/strings.h",
        "spectator/tags.h",
        "spectator/timer.h",
        "spectator/util.h",
        "spectator/version.h",
        "valid_chars.inc",
    ],
    visibility = ["//visibility:public"],
    local_defines = select({
        ":centos7": ["CENTOS7=1"],
        "//conditions:default": [],
    }),
    deps = [
        ":util",
        "@asio",
        "@com_github_cyan4973_xxhash//:xxhash",
        "@com_github_fmtlib_fmt//:fmtlib",
        "@com_github_skarupke_flat_hash_map//:flat_hash_map",
        "@com_github_tencent_rapidjson//:rapidjson",
        "@com_github_tessil_hopscotch_map//:hopscotch_map",
        "@com_google_absl//absl/container:flat_hash_map",
        "@com_google_absl//absl/synchronization",
        "@com_google_absl//absl/time",
        "@curl",
        "@net_zlib//:zlib",
    ],
)

cc_library(
    name = "dummy_cfg",
    srcs = ["spectator/sample_config.cc"],
    visibility = ["//visibility:public"],
    deps = [":spectator"],
)

cc_binary(
    name = "gen_perc_bucket_tags",
    srcs = ["tools/gen_perc_bucket_tags.cc"],
)

cc_binary(
    name = "gen_perc_bucket_values",
    srcs = ["tools/gen_perc_bucket_values.cc"],
)

cc_binary(
    name = "gen_valid_chars",
    srcs = ["tools/gen_valid_chars.cc"],
)

genrule(
    name = "gen_valid_chars_rule",
    outs = ["valid_chars.inc"],
    cmd = "$(location gen_valid_chars) > $@",
    tools = [
        "gen_valid_chars",
    ],
)

genrule(
    name = "gen_perc_bucket_tags_rule",
    outs = ["percentile_bucket_tags.inc"],
    cmd = "$(location gen_perc_bucket_tags) > $@",
    tools = [
        "gen_perc_bucket_tags",
    ],
)

genrule(
    name = "gen_perc_bucket_values_rule",
    outs = ["percentile_bucket_values.inc"],
    cmd = "$(location gen_perc_bucket_values) > $@",
    tools = [
        "gen_perc_bucket_values",
    ],
)

cc_test(
    name = "spectator_test",
    srcs = glob([
        "spectator/*test.cc",
        "spectator/http_server.*",
        "spectator/test_utils.*",
        "bin/test_main.cc",
    ]),
    deps = [
        ":dummy_cfg",
        ":spectator",
        "@com_github_bombela_backward//:backward",
        "@com_google_googletest//:gtest",
    ],
)

cc_test(
    name = "to_valid_chars",
    srcs = ["bench/to_valid_bench.cc"],
    deps = ["@com_google_benchmark//:benchmark"],
)

cc_test(
    name = "tags_bench",
    srcs = ["bench/bench_tags.cc"],
    deps = [
        ":spectator",
        "@com_google_benchmark//:benchmark",
    ],
)

cc_library(
    name = "spectatord",
    srcs = [
        "server/local_server.cc",
        "server/proc_utils.cc",
        "server/spectatord.cc",
        "server/udp_server.cc",
        "util/logger.cc",
    ],
    hdrs = [
        "server/expiring_cache.h",
        "server/handler.h",
        "server/local.h",
        "server/local_server.h",
        "server/proc_utils.h",
        "server/spectatord.h",
        "server/udp_server.h",
        "util/files.h",
        "util/logger.h",
    ],
    deps = [
        ":spectator",
        "@asio",
        "@com_github_fmtlib_fmt//:fmtlib",
        "@com_github_gabime_spdlog//:spdlog",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/synchronization",
    ],
)

cc_test(
    name = "spectatord_test",
    srcs = [
        "bin/test_main.cc",
        "server/proc_utils_test.cc",
        "server/spectatord_test.cc",
        "spectator/test_utils.h",
        "spectator/test_utils.cc"
    ],
    data = glob(["test-resources/*"]),
    deps = [
        ":spectatord",
        "@com_github_bombela_backward//:backward",
        "@com_google_googletest//:gtest",
    ],
)

cc_test(
    name = "parse_bench",
    srcs = ["bench/parse_bench.cc"],
    includes = ["server"],
    deps = [
        ":spectatord",
        "@com_google_benchmark//:benchmark",
    ],
)

cc_test(
    name = "ms_bench",
    srcs = ["bench/get_measurement_bench.cc"],
    includes = ["server"],
    deps = [
        ":spectatord",
        "@com_google_benchmark//:benchmark",
    ],
)

cc_test(
    name = "statsd_bench",
    srcs = ["bench/statsd_bench.cc"],
    includes = ["server"],
    deps = [
        ":spectatord",
        "@com_google_benchmark//:benchmark",
    ],
)

cc_test(
    name = "fmt_bench",
    srcs = ["bench/measurements_fmt_bench.cc"],
    includes = ["server"],
    deps = [
        ":spectatord",
        "@com_google_benchmark//:benchmark",
    ],
)

cc_binary(
    name = "spectatord_main",
    srcs = ["bin/spectatord_main.cc"],
    includes = ["server"],
    linkopts = select({
        "@bazel_tools//src/conditions:linux_x86_64": [
            "/usr/lib/x86_64-linux-gnu/libbfd.a",
            "/usr/lib/x86_64-linux-gnu/libiberty.a",
            "-ldl",
        ],
        "//conditions:default": [],
    }),
    local_defines = select({
        "@bazel_tools//src/conditions:linux_x86_64": ["BACKWARD_HAS_BFD=1"],
        "//conditions:default": [],
    }),
    deps = [
        ":spectatord",
        "@com_google_absl//absl/flags:parse",
        "@com_github_gabime_spdlog//:spdlog",
        "@com_github_bombela_backward//:backward",
    ] + select({
        ":nflx_internal": ["@nflx_spectator_cfg//:spectator_cfg"],
        "//conditions:default": [":dummy_cfg"],
    }),
)

cc_binary(
    name = "metrics_gen",
    srcs = [
        "server/local.h",
        "tools/metrics_gen.cc",
    ],
    includes = ["server"],
    linkopts = ["-pthread"],
    deps = [
        "@asio",
        "@com_github_fmtlib_fmt//:fmtlib",
        "@com_github_gabime_spdlog//:spdlog",
        "@com_google_absl//absl/strings",
    ],
)
